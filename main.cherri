#define color red
#define glyph apple
#define name melon
/* 
    melon host shortcut üçâ

    melon is an orthogonally persistent language designed for creating automations for Apple devices that run blazingly fast ‚ö°. It uses Siri Shortcuts as a backend, but allows for more complex and performant automations to be created compared to Shortcuts application itself.

    this shortcut is powered by cherri üçí.
*/

const MELON_VM_STATUS_SYSCALL = "syscall"
const MELON_VM_STATUS_HALTED = "halted"

const MELON_SOURCE_CODE = "http://localhost:5500/dist/?"
@MELON_VERSION = "1.0.0"
@MELON_MAX_PERSISTANCE = 2147483647

@code = "
    if(1==1) 
        syscall(\"is.workflow.actions.showresult\", \"lol\");
    
    let a = syscall(\"is.workflow.actions.prompt\", \"Enter a value\");

    if(a == \"lol\"){
        syscall(\"is.workflow.actions.showresult\", \"lol\");
    }else{
        syscall(\"is.workflow.actions.showresult\", \"not lol\");
    }
"
@code = base64Encode(code)

@url = url("{MELON_SOURCE_CODE}begin={code}")

@items
repeat MELON_MAX_PERSISTANCE {    
    
    @response = getWebpageContents( url )
    @response = getText(response)
    
    @image = response

    @response = base64Decode(response)
    @response = getDictionary(response)

    @status = getValue(response, "status")
    const status = getText(status)

    @state = getValue(response, "state")
    const state = getText(state)

    if status == MELON_VM_STATUS_SYSCALL {
        @syscall = getValue(response, "syscall")
        const syscall = getDictionary(syscall)
        
        @syscall_name = getValue(syscall, "name")
        const syscall_name = getText(syscall_name)
        
        const syscall_args = getValue(syscall, "args")

        if syscall_name == "is.workflow.actions.showresult" {
            @value = getListItem(syscall_args, 0)
            @value = getValue(value, "value")

            alert(value)
        }

        if syscall_name == "is.workflow.actions.prompt" {
            @text = getListItem(syscall_args, 0)
            @text = getValue(text, "value")
            @value = prompt(text)
        }     
    }  

    if status == MELON_VM_STATUS_HALTED {
        stop()
    }
    
    @url = url("{MELON_SOURCE_CODE}resume={image}&value={value}")
}